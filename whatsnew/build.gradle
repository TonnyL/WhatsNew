apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "guru.stefma.bintrayrelease"

android {
    compileSdkVersion versions.compile_sdk_version
    buildToolsVersion versions.build_tools_version

    defaultConfig {
        minSdkVersion versions.library_min_sdk_version
        targetSdkVersion versions.target_sdk_version

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            postprocessing {
                removeUnusedCode false
                removeUnusedResources false
                obfuscate false
                optimizeCode false
                proguardFile 'proguard-rules.pro'
            }
        }
    }

    lintOptions {
        abortOnError false
    }

}

androidExtensions {
    experimental = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:${versions.appcompat}"
    implementation "androidx.recyclerview:recyclerview:${versions.recycler_view}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    androidTestImplementation "androidx.test:core:${versions.test_core}"
    androidTestImplementation "androidx.test:runner:${versions.test_runner_rules}"
    androidTestImplementation "androidx.test:rules:${versions.test_runner_rules}"
    androidTestImplementation "androidx.test.ext:junit:${versions.ext_junit}"
    androidTestImplementation "androidx.test.espresso:espresso-core:${versions.espresso}"
}

repositories {
    mavenCentral()
}

// Disable creating javadocs
tasks.withType(Javadoc) {
    enabled = false
}

// ./gradlew clean build bintrayUpload -PbintrayUser=BINTRAY_USERNAME -PbintrayKey=BINTRAY_KEY -PdryRun=false
version = "0.1.2"
group = "io.github.tonnyl"
androidArtifact {
    artifactId = "bintrayrelease"
}
publish {
    userOrg = "tonnyl"
    desc = "WhatsNew automatically displays a short description of the new features when users update your app."
    website = "https://github.com/TonnyL/WhatsNew"
}
